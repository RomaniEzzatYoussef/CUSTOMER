*-----------------------------------------------------------------------------
*  DATE             : 23 DEC 2021
*  CLIENT           : BDC
*  MODULE			: Limit & CUSTOMER
*  AUTHOR			: Yasmine & ROMANI EZZAT
*-----------------------------------------------------------------------------
    SUBROUTINE V.MBSC.GUARANTOR.CUST.AUTH
*-----------------------------------------------------------------------------

    $INSERT I_COMMON
    $INSERT I_EQUATE
	$INSERT I_F.CUSTOMER
	$INSERT I_F.MBSC.GUARANTOR.PARAM
*----------------------------------------------------------------------------
	*DEBUG
*----------------------------------------------------------------------------

	FN.MBSC.GUARANTOR.PARAM = 'F.MBSC.GUARANTOR.PARAM'
	F.MBSC.GUARANTOR.PARAM  = ''
	CALL OPF(FN.MBSC.GUARANTOR.PARAM, F.MBSC.GUARANTOR.PARAM)
	
*----------------------------------------------------------------------------

	GUARANTOR.ID = ''
	CALL GET.LOC.REF(APPLICATION,"GUARANTOR",GUARANTOR.POS)
	GUARANTOR.ID = R.NEW(EB.CUS.LOCAL.REF)<1,GUARANTOR.POS>
	
	GUARANT.PERCE = ''
	CALL GET.LOC.REF(APPLICATION,"GUARANT.PERCE",GUARANT.PERCE.POS)
	GUARANT.PERCE = R.NEW(EB.CUS.LOCAL.REF)<1,GUARANT.PERCE.POS>
	
	GUARANT.AMOUN = ''
	CALL GET.LOC.REF(APPLICATION,"GUARANT.AMOUN",GUARANT.AMOUN.POS)
	GUARANT.AMOUN = R.NEW(EB.CUS.LOCAL.REF)<1,GUARANT.AMOUN.POS>
	
	GUARANTOR.LEN = DCOUNT(GUARANTOR.ID,SM)
	
	CURR.CUS.ID = ID.NEW

	IF GUARANTOR.ID NE '' THEN	
		I=1
		LOOP
		WHILE I LE GUARANTOR.LEN
			GUAR.ID  = FIELD(GUARANTOR.ID,SM,I)
			
			GUAR.CUSTOMER       = ''
			DATE.OF.GUARANTEE   = ''
			PERC.OF.GUARANTEE   = ''
			AMT.OF.GUARANTEE    = ''
			GUAR.COMPANY        = ''
			LOAN.ID             = ''
			LIMIT.REF           = ''
			
			CUSTOMER.GUARANTOR = ''
			DATE.GUARANTOR     = ''
			PERC.GUARANTOR     = ''
			AMT.GUARANTOR      = ''
			COM.GUARANTOR      = ''
			LOAN.GUARANTOR     = ''
			LIMIT.REF.GUARANTOR     = ''
			
			CALL F.READ(FN.MBSC.GUARANTOR.PARAM,GUAR.ID,R.MBSC.GUARANTOR,F.MBSC.GUARANTOR.PARAM,ERR.FN.MBSC.GUARANTOR.PARAM)
			
			IF R.MBSC.GUARANTOR NE '' THEN
				GUAR.CUSTOMER     = R.MBSC.GUARANTOR<GUAR.PARAM.CUSTOMER>
				DATE.OF.GUARANTEE = R.MBSC.GUARANTOR<GUAR.PARAM.DATE.OF.GUARANTEE>
				PERC.OF.GUARANTEE = R.MBSC.GUARANTOR<GUAR.PARAM.PERC.OF.GUARANTEE>
				AMT.OF.GUARANTEE  = R.MBSC.GUARANTOR<GUAR.PARAM.AMT.OF.GUARANTEE>
				GUAR.COMPANY      = R.MBSC.GUARANTOR<GUAR.PARAM.COMPANY>
				LOAN.ID           = R.MBSC.GUARANTOR<GUAR.PARAM.LOAN.ID>
				LIMIT.REF         = R.MBSC.GUARANTOR<GUAR.PARAM.LIMIT.REF>

				GUAR.CUSTOMER.LEN = DCOUNT(GUAR.CUSTOMER,VM)
				
				GCL=1
				LOOP
				WHILE GCL LE GUAR.CUSTOMER.LEN
					
					GUAR.CUS.ID  = ''
					DATE.GUAR    = ''
					PERC.GUAR    = ''
					AMT.GUAR     = ''
					COM.GUAR     = ''
					LOAN.GUAR    = ''
					LI.REF.GUAR  = ''

					GUAR.CUS.ID  = FIELD(GUAR.CUSTOMER,VM,GCL)
					DATE.GUAR    = FIELD(DATE.OF.GUARANTEE,VM,GCL)
					PERC.GUAR    = FIELD(PERC.OF.GUARANTEE,VM,GCL)
					AMT.GUAR     = FIELD(AMT.OF.GUARANTEE,VM,GCL)
					COM.GUAR     = FIELD(GUAR.COMPANY,VM,GCL)
					LOAN.GUAR    = FIELD(LOAN.ID,VM,GCL)
					LI.REF.GUAR  = FIELD(LIMIT.REF,VM,GCL)

					IF GUAR.CUS.ID NE CURR.CUS.ID THEN
						CUSTOMER.GUARANTOR  := GUAR.CUS.ID:@VM
						DATE.GUARANTOR      := DATE.GUAR:@VM
						PERC.GUARANTOR      := PERC.GUAR:@VM
						AMT.GUARANTOR       := AMT.GUAR:@VM
						COM.GUARANTOR       := COM.GUAR:@VM
						LOAN.GUARANTOR      := LOAN.GUAR:@VM
						LIMIT.REF.GUARANTOR := LI.REF.GUAR:@VM
					END
				GCL++	
				REPEAT
			END
			
			GUAR.CUSTOMER       = ''
			DATE.OF.GUARANTEE   = ''
			PERC.OF.GUARANTEE   = ''
			AMT.OF.GUARANTEE    = ''
			GUAR.COMPANY        = ''
			LOAN.ID             = ''
			LIMIT.REF           = ''

			GUAR.CUSTOMER       = CUSTOMER.GUARANTOR:CURR.CUS.ID
			DATE.OF.GUARANTEE   = DATE.GUARANTOR:TODAY
			PERC.OF.GUARANTEE   = PERC.GUARANTOR:FIELD(GUARANT.PERCE,SM,I)
			AMT.OF.GUARANTEE    = AMT.GUARANTOR:FIELD(GUARANT.AMOUN,SM,I)
			GUAR.COMPANY        = COM.GUARANTOR:R.NEW(EB.CUS.CO.CODE)
			LOAN.ID             = LOAN.GUARANTOR:""
			LIMIT.REF           = LIMIT.REF.GUARANTOR:""

			R.GUARANTOR<GUAR.PARAM.CUSTOMER>          = GUAR.CUSTOMER
			R.GUARANTOR<GUAR.PARAM.DATE.OF.GUARANTEE> = DATE.OF.GUARANTEE
			R.GUARANTOR<GUAR.PARAM.PERC.OF.GUARANTEE> = PERC.OF.GUARANTEE
			R.GUARANTOR<GUAR.PARAM.AMT.OF.GUARANTEE>  = AMT.OF.GUARANTEE
			R.GUARANTOR<GUAR.PARAM.COMPANY>           = GUAR.COMPANY
			R.GUARANTOR<GUAR.PARAM.LOAN.ID>           = LOAN.ID
			R.GUARANTOR<GUAR.PARAM.LIMIT.REF>         = LIMIT.REF
			
			IF GUAR.ID NE '' THEN
				WRITE R.GUARANTOR TO F.MBSC.GUARANTOR.PARAM, GUAR.ID
			END
			
		I++
		REPEAT
	END  
 
*------------------------------------------------------------------------------------------------------------------
RETURN
END
